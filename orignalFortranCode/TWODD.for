      PROGRAM TWODD
C
      COMMON/S1/PI,PR,PR1,PR2,CON,CONS
      COMMON/S2/SXXS,SXXN,SYYS,SYYN,SXYS,SXYN,UXS,UXN,UYS,UYN
      COMMON/S3/C(2000,2000),B(2000),D(2000)
C
      DIMENSION XM(1000),YM(1000),A(1000),COSBET(1000),SINBET(1000)
	DIMENSION KOD(1000)
C     DIMENSION TITLE(20)
      CHARACTER *80 TITLE
C
      OPEN (3,FILE= 'f60.txt',STATUS='OLD')
      OPEN (16,FILE='RESf60.txt',STATUS='UNKNOWN')
      READ (3,*)  TITLE
      WRITE (16,2)  TITLE
      READ (3,*) NUMBS,NUMOS,KSYM
      READ (3,*) PR,E,XSYM,YSYM
      READ (3,*) PXX,PYY,PXY
      WRITE (16,6) NUMBS,NUMOS
      GO TO (80,85,90,95),KSYM
   80 WRITE (16,7)
      GO TO 100
   85 WRITE (16,8) XSYM
      GO TO 100
   90 WRITE (16,9)YSYM
      GO TO 100
   95 WRITE (16,10) XSYM,YSYM
C
  100 CONTINUE
      WRITE (16,11) PR,E
      WRITE (16,12)PXX,PYY,PXY
C
      PI=4.*ATAN(1.)
      CON=1./(4.*PI*(1.-PR))
      CONS=E/(1.+PR)
      PR1=1.-2.*PR
      PR2=2.*(1.-PR)
C
C     DEFINE LOCATIONS, SIZES, ORIENTATIONS AND BOUNDARY CONDITIONS OF
C       BOUNDARY ELEMENTS.
C
      NUMBE=0
      DO 110 N=1,NUMBS
      READ (3,*)NUM,XBEG,YBEG,XEND,YEND,KODE,BVS,BVN
      XD=(XEND-XBEG)/NUM
      YD=(YEND-YBEG)/NUM
      SW=SQRT(XD*XD+YD*YD)
C
      DO 110 NE=1,NUM
      NUMBE=NUMBE+1
      M=NUMBE
      XM(M)=XBEG+0.5*(2.*NE-1.)*XD
      YM(M)=YBEG+0.5*(2.*NE-1.)*YD
      A(M)=0.5*SW
      SINBET(M)=YD/SW
      COSBET(M)=XD/SW
      KOD(M)=KODE
      MN=2*M
      MS=MN-1
      B(MS)=BVS
  110 B(MN)=BVN
C
      WRITE (16,13)
      DO 115 M=1,NUMBE
      SIZE=2.*A(M)
      ANGLE=180.*ATAN2(SINBET(M),COSBET(M))/PI
      WRITE (16,15) M,KOD(M),XM(M),YM(M),SIZE,ANGLE,B(2*M-1),B(2*M)
  115 CONTINUE
C
C     ADJUST STRESS BOUNDARY VALUES TO ACCOUNT FOR INITIAL STRESSES.
C
      DO 150 N=1,NUMBE
      NN=2*N
      NS=NN-1
      COSB=COSBET(N)
      SINB=SINBET(N)
      SIGS=(PYY-PXX)*SINB*COSB+PXY*(COSB*COSB-SINB*SINB)
      SIGN=PXX*SINB*SINB-2.*PXY*SINB*COSB+PYY*COSB*COSB
      GO TO (120,150,130,140),KOD(N)
  120 B(NS)=B(NS)-SIGS
      B(NN)=B(NN)-SIGN
      GO TO 150
  130 B(NN)=B(NN)-SIGN
      GO TO 150
  140 B(NS)=B(NS)-SIGS
  150 CONTINUE
C
C     COMPUTE INFLUENCE COEFFICIENTS AND SET UP SYSTEM OF ALGEBRAIC
C       EQUATIONS.
C
      DO 300 I=1,NUMBE
      IN=2*I
      IS=IN-1
      XI=XM(I)
      YI=YM(I)
      COSBI=COSBET(I)
      SINBI=SINBET(I)
      KODE=KOD(I)
C
      DO 300 J=1,NUMBE
      JN=2*J
      JS=JN-1
      CALL INITL
      XJ=XM(J)
      YJ=YM(J)
      COSBJ=COSBET(J)
      SINBJ=SINBET(J)
      AJ=A(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,COSBJ,SINBJ,+1)
      GO TO (240,210,220,230),KSYM
C
  210 XJ=2.*XSYM-XM(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,COSBJ,-SINBJ,-1)
      GO TO 240
C
  220 YJ=2.*YSYM-YM(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,-COSBJ,SINBJ,-1)
      GO TO 240
C
  230 XJ=2.*XSYM-XM(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,COSBJ,-SINBJ,-1)
      XJ=XM(J)
      YJ=2.*YSYM-YM(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,-COSBJ,SINBJ,-1)
      XJ=2.*XSYM-XM(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,-COSBJ,-SINBJ,+1)
C
  240 CONTINUE
      GO TO(250,260,270,280),KODE
C
  250 C(IS,JS)=(SYYS-SXXS)*SINBI*COSBI+SXYS*(COSBI*COSBI-SINBI*SINBI)
      C(IS,JN)=(SYYN-SXXN)*SINBI*COSBI+SXYN*(COSBI*COSBI-SINBI*SINBI)
      C(IN,JS)=SXXS*SINBI*SINBI-2.*SXYS*SINBI*COSBI+SYYS*COSBI*COSBI
      C(IN,JN)=SXXN*SINBI*SINBI-2.*SXYN*SINBI*COSBI+SYYN*COSBI*COSBI
      GO TO 300
C
  260 C(IS,JS)=UXS*COSBI+UYS*SINBI
      C(IS,JN)=UXN*COSBI+UYN*SINBI
      C(IN,JS)=-UXS*SINBI+UYS*COSBI
      C(IN,JN)=-UXN*SINBI+UYN*COSBI
      GO TO 300
C
  270 C(IS,JS)=UXS*COSBI+UYS*SINBI
      C(IS,JN)=UXN*COSBI+UYN*SINBI
      C(IN,JS)=SXXS*SINBI*SINBI-2.*SXYS*SINBI*COSBI+SYYS*COSBI*COSBI
      C(IN,JN)=SXXN*SINBI*SINBI-2.*SXYN*SINBI*COSBI+SYYN*COSBI*COSBI
      GO TO 300
C
  280 C(IS,JS)=(SYYS-SXXS)*SINBI*COSBI+SXYS*(COSBI*COSBI-SINBI*SINBI)
      C(IS,JN)=(SYYN-SXXN)*SINBI*COSBI+SXYN*(COSBI*COSBI-SINBI*SINBI)
      C(IN,JS)=-UXS*SINBI+UYS*COSBI
      C(IN,JN)=-UXN*SINBI+UYN*COSBI
C
  300 CONTINUE
C
C     SOLVE SYSTEM OF ALGEBRAIC EQUATIONS.
C
      N=2*NUMBE
      CALL SOLVE(N)
C
C     COMPUTE BOUNDARY DISPLACEMENTS AND STRESSES.
C
      WRITE(16,16)
      DO 600 I=1,NUMBE
      IN=2*I
      IS=IN-1
      XI=XM(I)
      YI=YM(I)
      COSBI=COSBET(I)
      SINBI=SINBET(I)
C
      UXNEG=0.
      UYNEG=0.
      SIGXX=PXX
      SIGYY=PYY
      SIGXY=PXY
C
      DO 570 J=1,NUMBE
      JN=2*J
      JS=JN-1
      CALL INITL
      XJ=XM(J)
      YJ=YM(J)
      AJ=A(J)
      COSBJ=COSBET(J)
      SINBJ=SINBET(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,COSBJ,SINBJ,+1)
      GO TO (540,510,520,530),KSYM
C
  510 XJ=2.*XSYM-XM(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,COSBJ,-SINBJ,-1)
      GO TO 540
C
  520 YJ=2.*YSYM-YM(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,-COSBJ,SINBJ,-1)
      GO TO 540
C
  530 XJ=2.*XSYM-XM(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,COSBJ,-SINBJ,-1)
      XJ=XM(J)
      YJ=2.*YSYM-YM(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,-COSBJ,SINBJ,-1)
      XJ=2.*XSYM-XM(J)
      CALL COEFF(XI,YI,XJ,YJ,AJ,-COSBJ,-SINBJ,+1)
C
  540 CONTINUE
C
      UXNEG=UXNEG+UXS*D(JS)+UXN*D(JN)
      UYNEG=UYNEG+UYS*D(JS)+UYN*D(JN)
      SIGXX=SIGXX+SXXS*D(JS)+SXXN*D(JN)
      SIGYY=SIGYY+SYYS*D(JS)+SYYN*D(JN)
      SIGXY=SIGXY+SXYS*D(JS)+SXYN*D(JN)
C
  570 CONTINUE
C
      USNEG=UXNEG*COSBI+UYNEG*SINBI
      UNNEG=-UXNEG*SINBI+UYNEG*COSBI
      USPOS=USNEG-D(IS)
      UNPOS=UNNEG-D(IN)
      UXPOS=USPOS*COSBI-UNPOS*SINBI
      UYPOS=USPOS*SINBI+UNPOS*COSBI
      SIGS=(SIGYY-SIGXX)*SINBI*COSBI+SIGXY*(COSBI*COSBI-SINBI*SINBI)
      SIGN=SIGXX*SINBI*SINBI-2.*SIGXY*SINBI*COSBI+SIGYY*COSBI*COSBI
C
      WRITE (16,17) I,D(IS),USNEG,USPOS,D(IN),UNNEG,UNPOS,UXNEG,UYNEG,
     1             UXPOS,UYPOS,SIGS,SIGN
  600 CONTINUE
C
C     COMPUTE DISPLACEMENTS AND STRESSES AT SPECIFIED POINTS IN BODY.
C
      IF (NUMOS.LE.0) GO TO 900
      WRITE (16,18)
      NPOINT=0
      DO 890 N=1,NUMOS
      READ (3,*)XBEG,YBEG,XEND,YEND,NUMPB
      NUMP=NUMPB+1
      DELX=(XEND-XBEG)/NUMP
      DELY=(YEND-YBEG)/NUMP
      IF (NUMPB.GT.0) NUMP=NUMP+1
      IF (DELX**2+DELY**2.EQ.0.) NUMP=1
C
      DO 890 NI=1,NUMP
      XP=XBEG+(NI-1)*DELX
      YP=YBEG+(NI-1)*DELY
C
      UX=0.
      UY=0.
      SIGXX=PXX
      SIGYY=PYY
      SIGXY=PXY
C
      DO 880 J=1,NUMBE
      JN=2*J
      JS=JN-1
      CALL INITL
      XJ=XM(J)
      YJ=YM(J)
      AJ=A(J)
C
      IF (SQRT((XP-XJ)**2+(YP-YJ)**2).LT.2.*AJ) GO TO 890
C
      COSBJ=COSBET(J)
      SINBJ=SINBET(J)
      CALL COEFF(XP,YP,XJ,YJ,AJ,COSBJ,SINBJ,+1)
      GO TO (840,810,820,830),KSYM
C
  810 XJ=2.*XSYM-XM(J)
      CALL COEFF(XP,YP,XJ,YJ,AJ,COSBJ,-SINBJ,-1)
      GO TO 840
C
  820 YJ=2.*YSYM-YM(J)
      CALL COEFF(XP,YP,XJ,YJ,AJ,-COSBJ,SINBJ,-1)
      GO TO 840
C
  830 XJ=2.*XSYM-XM(J)
      CALL COEFF(XP,YP,XJ,YJ,AJ,COSBJ,-SINBJ,-1)
      XJ=XM(J)
      YJ=2.*YSYM-YM(J)
      CALL COEFF(XP,YP,XJ,YJ,AJ,-COSBJ,SINBJ,-1)
      XJ=2.*XSYM-XM(J)
      CALL COEFF(XP,YP,XJ,YJ,AJ,-COSBJ,-SINBJ,+1)
C
  840 CONTINUE
C
      UX=UX+UXS*D(JS)+UXN*D(JN)
      UY=UY+UYS*D(JS)+UYN*D(JN)
      SIGXX=SIGXX+SXXS*D(JS)+SXXN*D(JN)
      SIGYY=SIGYY+SYYS*D(JS)+SYYN*D(JN)
      SIGXY=SIGXY+SXYS*D(JS)+SXYN*D(JN)
C
  880 CONTINUE
C
      NPOINT=NPOINT+1
      WRITE (16,20) NPOINT,XP,YP,UX,UY,SIGXX,SIGYY,SIGXY
C
  890 CONTINUE
C
  900 CONTINUE
C
C     FORMAT STATEMENTS.
C
C   1 FORMAT (20A4)
    2 FORMAT (1H1,/,25X,1A80,/)
C   3 FORMAT (3I4)
C   4 FORMAT (F6.2,E11.4,2F12.4)
C   5 FORMAT (3E11.4)
    6 FORMAT (/,' NUMBER OF STRAIGHT-LINE SEGMENTS (EACH CONTAINING AT
     1LEAST ONE BOUNDARY ELEMENT) USED TO DEFINE BOUNDARIES =',I3,//,
     2' NUMBER OF STRAIGHT-LINE SEGMENTS USED TO SPECIFY OTHER LOCATION
     3 (I.E., NOT ON A BOUNDARY) WHERE RESULTS ARE TO BE FOUND =',I3)
    7 FORMAT (/,32H NO SYMMETRY CONDITIONS IMPOSED.)
    8 FORMAT (/,18H THE LINE X = XS =,F12.4,23H IS A LINE OF SYYMETRY.)
    9 FORMAT (/,18H THE LINE Y = YS =,F12.4,23H IS A LINE OF SYMMETRY.)
   10 FORMAT (/,19H THE LINES X = XS =,F12.4,13H AND Y = YS =,F12.4,
     1 23H ARE LINES OF SYMMETRY.)
   11 FORMAT (/,18H POISSON'S RATIO =,F6.2,//,18H YOUNG'S MODULUS =,E11
     1.4)
   12 FORMAT (/,31H XX-COMPONENT OF FIELD STRESS =,ES20.4,//,8H YY-COMP,
     123HONENT OF FIELD STRESS =,ES20.4,//,25H XY-COMPONENT OF FIELD ST,
     26HRESS =,E11.4)
   13 FORMAT (1H1,/,27H     BOUNDARY ELEMENT DATA.,//,96H  ELEMENT
     1KODE  X (CENTER)  Y (CENTER)      LENGTH      ANGLE  US OR SIGMA_
     2  UN OR SIGMA_N,/)
C  14 FORMAT (I4,4F12.4,I4,2E11.4)
   15 FORMAT (2I9,3F12.4,ES12.2,2ES15.4)
   16 FORMAT (1H1,/,'     DISPLACEMENTS AND STRESSES AT MIDPOINTS OF BO
     1UNDARY ELEMENTS.',//,'   ELEMENT        DS     US(-)     US(+)
     2     DN     UN(-)     UN(+)     UX(-)     UY(-)     UX(+)     UY(
     3+)   SIGMA-S   SIGMA-N',/)
   17 FORMAT (I10,10ES15.3,2E15.3)
   18 FORMAT (1H1,/,'     DISPLACEMENTS AND STRESSES AT SPECIFIED POINT
     1S IN THE BODY.',//,'    POINT    X CO-ORD    Y CO-ORD          UX
     2          UY      SIGXX      SIGYY      SIGXY',/)
C  19 FORMAT (4F12.4,I4)
   20 FORMAT (I9,7E13.3 )
C
      CLOSE (3)
      CLOSE (16)
      write(*,*) 'Successful!'

      END


      SUBROUTINE INITL
C
      COMMON/S2/SXXS,SXXN,SYYS,SYYN,SXYS,SXYN,UXS,UXN,UYS,UYN
C
      SXXS=0.
      SXXN=0.
      SYYS=0.
      SYYN=0.
      SXYS=0.
      SXYN=0.
C
      UXS=0.
      UXN=0.
      UYS=0.
      UYN=0.
C
      RETURN
      END
      SUBROUTINE COEFF(X,Y,CX,CY,A,COSB,SINB,MSYM)
C
      COMMON/S1/PI,PR,PR1,PR2,CON,CONS
      COMMON/S2/SXXS,SXXN,SYYS,SYYN,SXYS,SXYN,UXS,UXN,UYS,UYN
C
      COS2B=COSB*COSB-SINB*SINB
      SIN2B=2.*SINB*COSB
      COSB2=COSB*COSB
      SINB2=SINB*SINB
C
      XB=(X-CX)*COSB+(Y-CY)*SINB
      YB=-(X-CX)*SINB+(Y-CY)*COSB
C
      R1S=(XB-A)*(XB-A)+YB*YB
      R2S=(XB+A)*(XB+A)+YB*YB
      FL1=0.5*ALOG(R1S)
      FL2=0.5*ALOG(R2S)
      FB2=CON*(FL1-FL2)
      IF (YB.NE.0.) GO TO 10
      FB3=0.
      IF (ABS(XB).LT.A) FB3=CON*PI
      GO TO 20
   10 FB3=-CON*(ATAN((XB+A)/YB)-ATAN((XB-A)/YB))
   20 FB4=CON*(YB/R1S-YB/R2S)
      FB5=CON*((XB-A)/R1S-(XB+A)/R2S)
      FB6=CON*(((XB-A)**2-YB*YB)/R1S**2-((XB+A)**2-YB*YB)/R2S**2)
      FB7=2.*CON*YB*((XB-A)/R1S**2-(XB+A)/R2S**2)
C
      UXDS=-PR1*SINB*FB2+PR2*COSB*FB3+YB*(SINB*FB4-COSB*FB5)
      UXDN=-PR1*COSB*FB2-PR2*SINB*FB3-YB*(COSB*FB4+SINB*FB5)
      UYDS=PR1*COSB*FB2+PR2*SINB*FB3-YB*(COSB*FB4+SINB*FB5)
      UYDN=-PR1*SINB*FB2+PR2*COSB*FB3-YB*(SINB*FB4-COSB*FB5)
C
      SXXDS=CONS*(2.*COSB2*FB4+SIN2B*FB5+YB*(COS2B*FB6-SIN2B*FB7))
      SXXDN=CONS*(-FB5+YB*(SIN2B*FB6+COS2B*FB7))
      SYYDS=CONS*(2.*SINB2*FB4-SIN2B*FB5-YB*(COS2B*FB6-SIN2B*FB7))
      SYYDN=CONS*(-FB5-YB*(SIN2B*FB6+COS2B*FB7))
      SXYDS=CONS*(SIN2B*FB4-COS2B*FB5+YB*(SIN2B*FB6+COS2B*FB7))
      SXYDN=CONS*(-YB*(COS2B*FB6-SIN2B*FB7))
C
      UXS=UXS+MSYM*UXDS
      UXN=UXN+UXDN
      UYS=UYS+MSYM*UYDS
      UYN=UYN+UYDN
C
      SXXS=SXXS+MSYM*SXXDS
      SXXN=SXXN+SXXDN
      SYYS=SYYS+MSYM*SYYDS
      SYYN=SYYN+SYYDN
      SXYS=SXYS+MSYM*SXYDS
      SXYN=SXYN+SXYDN
C
      RETURN
      END
      SUBROUTINE SOLVE(N)
C
      COMMON/S3/A(2000,2000),B(2000),X(2000)
C
      NB=N-1
      DO 20 J=1,NB
      L=J+1
      DO 20 JJ=L,N
      XM=A(JJ,J)/A(J,J)
      DO 10 I=J,N
   10 A(JJ,I)=A(JJ,I)-A(J,I)*XM
   20 B(JJ)=B(JJ)-B(J)*XM
C
      X(N)=B(N)/A(N,N)
      DO 40 J=1,NB
      JJ=N-J
      L=JJ+1
      SUM=0.
      DO 30 I=L,N
   30 SUM=SUM+A(JJ,I)*X(I)
   40 X(JJ)=(B(JJ)-SUM)/A(JJ,JJ)
C
      RETURN
      END

